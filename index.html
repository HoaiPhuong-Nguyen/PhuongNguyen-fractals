<!DOCTYPE html>
 <html>
 <head>
     <title>The magic of fractals</title>
	 <meta charset="utf-8" />
	<link href='https://fonts.googleapis.com/css?family=Lato:400,300,100,700,900' rel='stylesheet' type='text/css'>
	<link rel="stylesheet" type="text/css" href="style1.css"/>
</head>
<body>
<header><h1 class="page-title">The magic of fractals</h1>
<div class="page-description">With infinitely repeating patterns and a finitely bounded area, fractals carry the paradox of the universe in itself. It is no doubt that human beings 
are fascinated by its properties, since knowledge about fractals seems to be the closest thing to infinity that human can grasp. Yet, such transcendentally mathematical concept like fractals
appear everyday in our routinely life: snowflakes carry fractals in their patterns of formation, a voltage shock also creates patterns on the surfaces that it strikes, etc. Therefore, knowledge
about fractals are not only theoretical, but also practical. Below are 3 basic ways to generate fractals, modified by processing-javascript codes and colorful transitions. I hope both
your intellectual and aesthetic needs are satisfied! </div>
</header>
<br>

      <script src="processing.js"></script>      
<div class = "scripts"><script type="application/processing" data-processing-target="pjs">
      float theta;   

void setup() {
  size(640, 360);
}

void draw() {
  background(0);
  frameRate(80);
  stroke(random(0,255), random(0,255), random(0,255));
  // Let's pick an angle 0 to 90 degrees based on the mouse position
  float a =  /*(mouseY / (float) height)*/60f;
  // Convert it to radians
  theta = radians(a);
  // Start the tree from the bottom of the screen
  translate(width/2,height-30-80);
  // Draw a line 120 pixels
  line(0,0,0,-190);
  branch(120);

}

void branch(float h) {
  // Each branch will be 2/3rds the size of the previous one
  h *= 0.7;
  
  // All recursive functions must have an exit condition!!!!
  // Here, ours is when the length of the branch is 2 pixels or less
  if (h > 0.9) {
    stroke(random(0,255), random(0,255), random(0,255));
    pushMatrix();    // Save the current state of transformation (i.e. where are we now)
    rotate(theta);   // Rotate by theta
    line(0, 0, 0, -h);  // Draw the branch
    translate(0, -h); // Move to the end of the branch
    branch(h);       // Ok, now call myself to draw two new branches!!
    popMatrix();     // Whenever we get back here, we "pop" in order to restore the previous matrix state
    
    // Repeat the same thing, only branch off to the "left" this time!
    stroke(random(0,255), random(0,255), random(0,255));
    pushMatrix();
    rotate(-theta);
    line(0, 0, 0, -h);
    translate(0, -h);
    branch(h);
    popMatrix();
  }
}
     </script>
	 <canvas id="pjs"> </canvas>
	 <ul id="hovermenu">
  <li>Finite subdivision rules -- Sierpinski carpet
    <ul class="dropdown">
      <li><div class = "des">Similar to the process of cell division, Sierpinski carpet
	  can be most representative for that beautiful pattern the Finite Subdivision rules. It can be create by using a recursive topological algorithm.
In real life, it is most significant in religious architecture because of its symmetric characteristics.</div></li>
    </ul>
  </li>
</ul>
	 <script src="processing.js"></script>      
<script type="application/processing" data-processing-target="pjs1">
size(640, 360);
noLoop();
background(0);

float w = 4;
float h = (w * height) / width;


float xmin = -w/2;
float ymin = -h/2;


loadPixels();


int maxiterations = 100;


float xmax = xmin + w;

float ymax = ymin + h;


float dx = (xmax - xmin) / (width);
float dy = (ymax - ymin) / (height);


float y = ymin;
for (int j = 0; j < height; j++) {
  
  float x = xmin;
  for (int i = 0; i < width; i++) {

    
    float a = x;
    float b = y;
    int n = 0;
    float max = 4.0;  // Infinity in our finite world is simple, let's just consider it 4
    float absOld = 0.0;
    float convergeNumber = maxiterations; // this will change if the while loop breaks due to non-convergence
    while (n < maxiterations) {
      // We suppose z = a+ib
      float aa = a * a;
      float bb = b * b;
      float abs = sqrt(aa + bb);
      if (abs > max) { // |z| = sqrt(a^2+b^2)
        // Now measure how much we exceeded the maximum: 
        float diffToLast = (float) (abs - absOld);
        float diffToMax  = (float) (max - absOld);
        convergeNumber = n + diffToMax/diffToLast;
        break;  // Bail
      }
      float twoab = 2.0 * a * b;
      a = aa - bb + x; // this operation corresponds to z -> z^2+c where z=a+ib c=(x,y)
      b = twoab + y;
      n++;
      absOld = abs;
    }

   
    if (n == maxiterations) {
      pixels[i+j*width] = color(random(0,255), random(0,255), random(0,255));
    } else {
      float norm = map(convergeNumber, 0, maxiterations, 0, 1);
      pixels[i+j*width] = color(map(sqrt(norm), 0, 1, 0, 255));
    }
    x += dx;
  }
  y += dy;
}
updatePixels();
</script>
<canvas id="pjs1"> </canvas>
<ul id="hovermenu">
  <li>Escape-time fractal -- the Mandelbrot set
    <ul class="dropdown">
      <li><div class = "des">With its surreal name, this fractal generator intrigues several mathematicians. These fractals are generated by iterating a formula on each point 
	  in a given space. 
	  If a point diverges as the formula is iterated, it escapes; otherwise, it remains bounded. Its representative example -- the Mandelbrot set, as been used several times
to prove how magical the self-similarity property of fractals can be.	  </div></li>
    </ul>
  </li>
</ul>
<script src="processing.js"></script>      
<script type="application/processing" data-processing-target="pjs2">
KochFractal k;



void setup() {

  size(640,360);

  background(0);

  frameRate(1);  // Animate slowly

  k = new KochFractal();

  smooth();

}



void draw() {

  background(0);

  // Draws the snowflake!

  k.render();

  // Iterate

  k.nextLevel();

  // Let's not do it more than 5 times. . .

  if (k.getCount() > 5) {

    k.restart();

  }



}





// A class to manage the list of line segments in the snowflake pattern



class KochFractal {

  Point start;       // A point for the start

  Point end;         // A point for the end

  ArrayList lines;   // A list to keep track of all the lines

  int count;

  

  public KochFractal()

  {

    start = new Point(0,height/2 + height/4);

    end = new Point(width,height/2  + height/4);

    lines = new ArrayList();

    restart();

  }



  void nextLevel()

  {  

    // For every line that is in the arraylist

    // create 4 more lines in a new arraylist

    lines = iterate(lines);

    count++;

  }



  void restart()

  { 

    count = 0;      // Reset count

    lines.clear();  // Empty the array list

    lines.add(new KochLine(start,end));  // Add the initial line (from one end point to the other)

  }

  

  int getCount() {

    return count;

  }

  

  // This is easy, just draw all the lines

  void render()

  {

    for(int i = 0; i < lines.size(); i++) {

      KochLine l = (KochLine)lines.get(i);

      l.render();

    }

  }



  // This is where the **MAGIC** happens

  // Step 1: Create an empty arraylist

  // Step 2: For every line currently in the arraylist

  //   - calculate 4 line segments based on Koch algorithm

  //   - add all 4 line segments into the new arraylist

  // Step 3: Return the new arraylist and it becomes the list of line segments for the structure

  

  // As we do this over and over again, each line gets broken into 4 lines, which gets broken into 4 lines, and so on. . . 

  ArrayList iterate(ArrayList before)

  {

    ArrayList now = new ArrayList();    //Create emtpy list

    for(int i = 0; i < before.size(); i++)

    {

      KochLine l = (KochLine)lines.get(i);   // A line segment inside the list

      // Calculate 5 koch points (done for us by the line object)

      Point a = l.start();                 

      Point b = l.kochleft();

      Point c = l.kochmiddle();

      Point d = l.kochright();

      Point e = l.end();

      // Make line segments between all the points and add them

      now.add(new KochLine(a,b));

      now.add(new KochLine(b,c));

      now.add(new KochLine(c,d));

      now.add(new KochLine(d,e));

    }

    return now;

  }



}





// A class to describe one line segment in the fractal

// Includes methods to calculate midpoints along the line according to the Koch algorithm



class KochLine {

  

  // Two points,

  // a is the "left" point and 

  // b is the "right point

  Point a,b;

  

  KochLine(Point a_, Point b_) {

     a = a_.copy();

     b = b_.copy();

  }

  

  void render() {

    stroke(random(0,255), random(0,255), random(0,255));

    line(a.x,a.y,b.x,b.y);

  }

  

  Point start() {

    return a.copy();

  }

  

  Point end() {

    return b.copy();

  }

      

  // This is easy, just 1/3 of the way

  Point kochleft()

  {

    float x = a.x + (b.x - a.x) / 3f;

    float y = a.y + (b.y - a.y) / 3f;

    return new Point(x,y);

  }    

  

  // More complicated, have to use a little trig to figure out where this point is!

  Point kochmiddle()

  {

    float x = a.x + 0.5f * (b.x - a.x) + (sin(radians(60))*(b.y-a.y)) / 3;

    float y = a.y + 0.5f * (b.y - a.y) - (sin(radians(60))*(b.x-a.x)) / 3;

    return new Point(x,y);

  }    



  // Easy, just 2/3 of the way

  Point kochright()

  {

    float x = a.x + 2*(b.x - a.x) / 3f;

    float y = a.y + 2*(b.y - a.y) / 3f;

    return new Point(x,y);

  }    



}



class Point {

  float x,y;

  

  Point(float x_, float y_) {

    x = x_;

    y = y_;

  }

  

  Point copy() {

    return new Point(x,y);

  }

}
</script>
<canvas id="pjs2"></canvas>
<ul id="hovermenu">
  <li>Iterated function systems -- Koch snowflake
    <ul class="dropdown">
      <li><div class ="des">With its representation as a snowflake, which hits close to many homes, the Iterated function systems 
	  carries the paradox of nature: an infinitely repeated pattern with finite area. Hutchinson (1981) showed that, for the metric space, 
	 a system of functions has a unique nonempty compact (closed and bounded) fixed set S. 
	 It is made up of the union of several copies of itself, each copy being transformed by a function.
	 They bring points closer together and make shapes smaller, and is used as the most common introduction to fractals.</div></li>
    </ul>
  </li>
</ul>
</div>
</body>
</html>